class_name CardManager extends Node2D

@export var player_hand: Hand
@export var players_deck: Deck

@export var enemy_hand: Hand
@export var enemy_deck: Deck

const COLLISION_MASK_CARD = 1
const COLLISION_MASK_CARD_SLOT = 2
const COLLISION_MASK_DECK = 4
const DEFAULT_CARD_MOVE_SPEED = 0.1

var card_being_dragged
var screen_size
var is_hovering_on_card

func _ready():
	screen_size = get_viewport_rect().size

func _process(delta) -> void:
	if card_being_dragged:
		var mouse_pos = get_global_mouse_position()
		card_being_dragged.position = Vector2(
			clamp(mouse_pos.x,0,screen_size.x),
			clamp(mouse_pos.y,0,screen_size.y),
			)

func _input(event):
	if event.is_action_pressed('left_click'):
		var card = raycast_check_for_card()
		if card:
			start_drag(card)
		ray_at_cursor()
	
	if event.is_action_released('left_click'):
		if card_being_dragged:
			finish_drag()
		
func start_drag(card):
	card_being_dragged = card
	card.scale = Vector2(1.5,1.5)

func finish_drag():
	card_being_dragged.scale = Vector2(1.1,1.1)
	var card_slot_found = raycast_check_for_card_slot()
	if card_slot_found and not card_slot_found.card_in_slot:
		player_hand.remove_card_from_hand(card_being_dragged)
		card_being_dragged.position = card_slot_found.position
		card_being_dragged.get_node("Area2D/CollisionPolygon2D").disabled = true
		card_slot_found.card_in_slot = true
	else:
		player_hand.add_card_to_hand(card_being_dragged, DEFAULT_CARD_MOVE_SPEED)
	card_being_dragged = null
		
func connect_card_signals(card):
	card.connect("card_hover_active", on_card_hover_active)
	card.connect("card_hover_inactive", on_card_hover_inactive)
	
func on_card_hover_active(card):
	if !is_hovering_on_card:
		is_hovering_on_card = true
		highlight_hovered_card(card, true)

func on_card_hover_inactive(card):
	if !card_being_dragged:
		highlight_hovered_card(card, false)
		var new_card_hovered = raycast_check_for_card()
		if new_card_hovered:
			highlight_hovered_card(new_card_hovered, self)
		else:
			is_hovering_on_card = false

func highlight_hovered_card(card, hovered):
	if hovered:
		card.scale = Vector2(1.1, 1.1)
#		Changes the layer in which the card is renderd infront or behind other cards
		card.z_index = 2
	else:
		card.scale = Vector2(1, 1)
		card.z_index = 1
		
func ray_at_cursor():
	var space_state = get_world_2d().direct_space_state
	var parameters = PhysicsPointQueryParameters2D.new()
	
	parameters.position = get_global_mouse_position()
	parameters.collide_with_areas = true
	var result = space_state.intersect_point(parameters)
	
	if result.size() > 0:
		var result_collision_mask = result[0].collider.collision_mask
		if result_collision_mask == COLLISION_MASK_DECK:
			var deck: Deck = result[0].collider.get_parent()
			if deck.belongs_to_player:
				print("drawing from player's deck")
				players_deck.draw_card()
			else:
				print("drawing from enemy's deck")
				enemy_deck.draw_card()
	

func raycast_check_for_card_slot():
	var space_state = get_world_2d().direct_space_state
	var parameters = PhysicsPointQueryParameters2D.new()
	
	parameters.position = get_global_mouse_position()
	parameters.collide_with_areas = true
	parameters.collision_mask = COLLISION_MASK_CARD_SLOT
	
	var result = space_state.intersect_point(parameters)
	
	if result.size() > 0:
		return result[0].collider.get_parent()
	return null

func raycast_check_for_card():
	var space_state = get_world_2d().direct_space_state
	var parameters = PhysicsPointQueryParameters2D.new()
	
	parameters.position = get_global_mouse_position()
	parameters.collide_with_areas = true
	parameters.collision_mask = COLLISION_MASK_CARD
	
	var result = space_state.intersect_point(parameters)
	
	if result.size() > 0:
		return highest_z_index_card(result)
	return null

func highest_z_index_card(cards):
	var highest_z_card = cards[0].collider.get_parent()
	var highest_z_index = highest_z_card.z_index
	
	#Loop through cards and find highest z
	for i in range(1, cards.size()):
		var current_card = cards[1].collider.get_parent()
		if current_card.z_index > highest_z_index:
			highest_z_card = current_card
			highest_z_index = current_card.z_index
	return highest_z_card
